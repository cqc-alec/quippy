@@grammar :: quipper
@@whitespace :: /[^\S\n]*/

start :: Program = input:iostatement newline statements:{statement newline} ouput:iostatement $ ;

iostatement::IOStatement = ("Inputs"|"Outputs")":" qubits:",".{ioqubit}+ ;
ioqubit = qubit:int ":" "Qbit" ;

statement
    =
    | qgate
    | qrot
    | comment
    ;

qgate::QGate = "QGate[" ~ name:string "]" inverse:["*"] "(" qubit:int ") with" control:(controlled | no_control) ;
qrot::QRot = "QRot[" ~ string "," double "](" int ")" ;

comment::Comment = "Comment[" ~ text:string "](" wires:",".{wire}+ ")" ;
wire = qubit:int ":" text:string ;

controlled::Controlled = "controls=[" control:control "] with nocontrol" ;
no_control::NoControl = "nocontrol" ;

control::Control
    =
    | direction:"-" qubit:int
    | direction:"+" qubit:int
    ;

string = /"[^"]+"/ ;
int = /\d+/ ;
double = /(-)?\d+\.\d+e(-)?\d\d/ ;
newline = /\n/ ;
